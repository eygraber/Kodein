buildscript {

    repositories {
        jcenter()
    }

    ext.kotlin_version = '1.0.6'
    ext.junit_version = '4.12'

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.13"

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.12'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

def getClassPath(String p) { return project(p).sourceSets.main.compileClasspath.collect { it.absolutePath } }
def getAndroidClassPath(String p) { return project(p).android.getBootClasspath() +  project(p).android.libraryVariants.javaCompile.classpath.collect { it.collect { it.absolutePath } } .flatten() }

task("classpath") {
    doLast {
        println((
                getClassPath(':kodein')
            +   getClassPath(':kodein-conf')
            +   getClassPath(':kodein-core')
            +   getClassPath(':kodein-erased')
            +   getAndroidClassPath(':kodein-android')
        ).join(":"))
    }
}

// used for Android builds on travis
ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = System.getProperty("pre-dex", "true") == "true"
}
